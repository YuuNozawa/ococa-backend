plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'dev.ococa'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'	
	implementation"org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0"

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	
	testCompileOnly 'org.projectlombok:lombok:1.18.30'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}
tasks.withType(JavaCompile) {
    options.compilerArgs += ["-Xlint:-deprecation"]
}

tasks.named('test') {
	useJUnitPlatform()
}

task goOcoca() {
	group = 'Application Runners'
	description = 'go ococa!'
	doLast {
		println """
		        _   _
			O c O  ca
		"""
	}
}

task goOcocaDevMode(type: JavaExec) {
	group = 'Application Runners'
	description = 'Runs the application with the development profile'
	main = 'dev.ococa.api.App'
	classpath = sourceSets.main.runtimeClasspath
	systemProperty 'spring.profiles.active', 'dev'
}

task goOcocaProdMode(type: JavaExec) {
	group = 'Application Runners'
	description = 'Runs the application with the production profile'
	main = 'dev.ococa.api.App'
	classpath = sourceSets.main.runtimeClasspath
	systemProperty 'spring.profiles.active', 'prod'
}

test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        // 標準出力や標準エラーも出力したい場合は以下を有効に
        showStandardStreams = true
    }
}